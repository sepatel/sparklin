group 'io.tekniq.web'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.researchgate:gradle-release:2.4.0'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'net.researchgate.release'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.4"
    compile "com.sparkjava:spark-core:2.5.1"
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name 'sparklin'
                packaging 'jar'
                artifactId 'tekniq-sparklin'
                description 'A Kotlin HTTP Framework built on top of Spark with a DSL as easy to use as NodeJS\'s Express or Ruby\'s Sinatra'
                url 'https://github.com/sepatel/sparklin'

                scm {
                    connection 'scm:git:git://github.com/sepatel/sparklin.git'
                    developerConnection 'scm:git:git://github.com/sepatel/sparklin'
                    url 'http://github.com/sepatel/sparklin'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.githubusercontent.com/sepatel/sparklin/master/LICENSE.md'
                    }
                }

                developers {
                    developer {
                        id 'sepatel'
                        name 'Sejal Patel'
                        email 'sejal@tekniq.io'
                    }
                }
            }
        }
    }
}
